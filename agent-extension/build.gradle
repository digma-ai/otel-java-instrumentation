plugins {
    id "java"
    id "maven-publish"
    id "signing"

    /*
    Instrumentation agent extension mechanism expects a single jar containing everything required
    for your extension. This also includes any external libraries that your extension uses and
    cannot access from application classpath (see comment below about `javax.servlet-api` dependency).

    Thus we use Shadow Gradle plugin to package our classes and all required runtime dependencies
    into a single jar.
    See https://imperceptiblethoughts.com/shadow/ for more details about Shadow plugin.
     */
    id "com.github.johnrengelman.shadow" version "8.1.1"

    id "io.opentelemetry.instrumentation.muzzle-generation" version "2.2.0-alpha"
    id "io.opentelemetry.instrumentation.muzzle-check" version "2.2.0-alpha"
}

def vArtifactId = "digma-otel-agent-extension"
project.description = "Digma OpenTelemetry agent extension"

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
    withJavadocJar()
    withSourcesJar()
}

shadowJar {
    // archiveClassifier = '' <==> skips the file extension of "-all"
    archiveClassifier = ''

//    dependencies {
//
//        //just another safety not to include any otel dependencies in the shaded jar
//        exclude(dependency {
//            it.moduleGroup.contains('io.opentelemetry')
//        } as Spec<? super ResolvedDependency>)
//
//        exclude(dependency('io.opentelemetry:') as Spec<? super ResolvedDependency>)
//        exclude(dependency('io.opentelemetry.instrumentation:') as Spec<? super ResolvedDependency>)
//        exclude(dependency('io.opentelemetry.semconv:') as Spec<? super ResolvedDependency>)
//    }
}

base.archivesName.set(vArtifactId)

ext {
    versions = [
            // this line is managed by .github/scripts/update-sdk-version.sh
            opentelemetrySdk           : "1.36.0",

            // these lines are managed by .github/scripts/update-version.sh
            opentelemetryJavaagent     : "2.2.0",
            opentelemetryJavaagentAlpha: "2.2.0-alpha",

            junit                      : "5.10.2",

            // various instrumentations
            springFramework            : "5.3.24",
            springKafka                : "2.7.1",
            grpc                       : "1.6.0",
    ]

    deps = [
            autoservice: dependencies.create(group: 'com.google.auto.service', name: 'auto-service', version: '1.1.1')
    ]
}

configurations {
    /*
    We create a separate gradle configuration to grab a published Otel instrumentation agent.
    We don't need the agent during development of this extension module.
    This agent is used only during integration test.
    */
    otel
}

dependencies {
    implementation(platform("io.opentelemetry:opentelemetry-bom:${versions.opentelemetrySdk}"))

    // these serve as a test of the instrumentation boms
    implementation(platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:${versions.opentelemetryJavaagent}"))
    implementation(platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:${versions.opentelemetryJavaagentAlpha}"))

    /*
    Interfaces and SPIs that we implement. We use `compileOnly` dependency because during
    runtime all necessary classes are provided by javaagent itself.
     */
    compileOnly("io.opentelemetry:opentelemetry-sdk-extension-autoconfigure-spi")
    compileOnly("io.opentelemetry.instrumentation:opentelemetry-instrumentation-api")
    compileOnly("io.opentelemetry.javaagent:opentelemetry-javaagent-extension-api")
    compileOnly("io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations-support")
    compileOnly("io.opentelemetry.instrumentation:opentelemetry-instrumentation-api-semconv")

    //Provides @AutoService annotation that makes registration of our SPI implementations much easier
    compileOnly deps.autoservice
    annotationProcessor deps.autoservice

    /*
     Used by our demo instrumentation module to reference classes of the target instrumented library.
     We again use `compileOnly` here because during runtime these classes are provided by the
     actual application that we instrument.

     NB! Only Advice (and "helper") classes of instrumentation modules can access classes from application classpath.
     See https://github.com/open-telemetry/opentelemetry-java-instrumentation/blob/main/docs/contributing/writing-instrumentation-module.md#advice-classes
     */
    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'


    compileOnly("io.grpc:grpc-core:${versions.grpc}")

    /*
    This dependency is required for DemoSpanProcessor both during compile and runtime.
    Only dependencies added to `implementation` configuration will be picked up by Shadow plugin
    and added to the resulting jar for our extension's distribution.
     */
    implementation project(':extension-version')
    implementation project(':instrumentation:common') // <==> io.github.digma-ai:digma-otel-instr-common
    implementation project(':instrumentation:grpc-16:library') // <==> io.github.digma-ai:digma-otel-instr-grpc


    //All dependencies below are only for tests
    testImplementation("org.testcontainers:testcontainers:1.19.7")
    testImplementation("com.fasterxml.jackson.core:jackson-databind:2.17.0")
    testImplementation("com.google.protobuf:protobuf-java-util:3.25.3")
    testImplementation("com.squareup.okhttp3:okhttp:4.12.0")
    testImplementation("io.opentelemetry:opentelemetry-api")
    testImplementation("io.opentelemetry.proto:opentelemetry-proto:1.1.0-alpha")

    testImplementation("org.junit.jupiter:junit-jupiter-api:${versions.junit}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${versions.junit}")
    testRuntimeOnly("ch.qos.logback:logback-classic:1.5.3")

    //Otel Java instrumentation that we use and extend during integration tests
    otel("io.opentelemetry.javaagent:opentelemetry-javaagent:${versions.opentelemetryJavaagent}")

    //Various libraries to test their instrumentations
    testImplementation("org.springframework:spring-context:${versions.springFramework}")
    testImplementation("org.springframework:spring-web:${versions.springFramework}")
    testImplementation("org.springframework.kafka:spring-kafka:${versions.springKafka}")

    //TODO remove when start using io.opentelemetry.instrumentation.javaagent-instrumentation plugin
    add("codegen", "io.opentelemetry.javaagent:opentelemetry-javaagent-tooling:${versions.opentelemetryJavaagentAlpha}")
    add("muzzleBootstrap", "io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations-support:${versions.opentelemetryJavaagentAlpha}")
    add("muzzleTooling", "io.opentelemetry.javaagent:opentelemetry-javaagent-extension-api:${versions.opentelemetryJavaagentAlpha}")
    add("muzzleTooling", "io.opentelemetry.javaagent:opentelemetry-javaagent-tooling:${versions.opentelemetryJavaagentAlpha}")
}

//Produces a copy of upstream javaagent with this extension jar included inside it
//The location of extension directory inside agent jar is hard-coded in the agent source code
task extendedAgent(type: Jar) {
    dependsOn(configurations.otel)
    dependsOn(jar)
    archiveFileName = "opentelemetry-javaagent.jar"
    from zipTree(configurations.otel.singleFile)
    from(tasks.shadowJar.archiveFile) {
        into "extensions"
    }

    //Preserve MANIFEST.MF file from the upstream javaagent
    doFirst {
        manifest.from(
                zipTree(configurations.otel.singleFile).matching {
                    include 'META-INF/MANIFEST.MF'
                }.singleFile
        )
    }
}

tasks {
    test {
        useJUnitPlatform()

        inputs.files(layout.files(tasks.shadowJar))
        inputs.files(layout.files(tasks.extendedAgent))

        systemProperty 'io.opentelemetry.smoketest.agentPath', configurations.otel.singleFile.absolutePath
        systemProperty 'io.opentelemetry.smoketest.extendedAgentPath', tasks.extendedAgent.archiveFile.get().asFile.absolutePath
        systemProperty 'io.opentelemetry.smoketest.extensionPath', tasks.shadowJar.archiveFile.get().asFile.absolutePath
    }

    compileJava {
        options.release.set(8)
    }

    assemble.dependsOn(shadowJar)
}

jar {
    manifest {
        attributes(
                'Implementation-Title': "Agent extension for OpenTelemetry by Digma",
                'Implementation-Vendor': "Digma",
                'Implementation-Version': version,
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': java.time.ZonedDateTime.now(java.time.ZoneOffset.UTC).format(java.time.format.DateTimeFormatter.ISO_ZONED_DATE_TIME),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

publishing {
    publications {
        DigmaOtelAgentExtension(MavenPublication) {
            artifactId = vArtifactId

            pom {
                name.set(vArtifactId)
                description.set(project.description)
                url.set("https://github.com/digma-ai/otel-java-instrumentation")

                licenses {
                    license {
                        name.set("MIT License")
                        url.set("http://www.opensource.org/licenses/mit-license.php")
                    }
                }
                developers {
                    developer {
                        name.set("Arik Sher")
                        email.set("asher@digma.ai")
                        organization.set("digma.ai")
                        organizationUrl.set("http://digma.ai/")
                    }
                }
                scm {
                    connection.set("scm:git:git://github.com/digma-ai/otel-java-instrumentation.git")
                    developerConnection.set("scm:git:ssh://github.com:digma-ai/otel-java-instrumentation.git")
                    url.set("https://github.com/digma-ai/otel-java-instrumentation")
                }
            }

            from components.java
        }
    }
    repositories {
        maven {
            if (version.toString().endsWith("SNAPSHOT")) {
                url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
                mavenContent {
                    snapshotsOnly()
                }
            } else {
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                mavenContent {
                    releasesOnly()
                }
            }
            credentials {
                username = System.getenv("EV_ossrhUsername")
                password = System.getenv("EV_ossrhPassword")
            }
        }

    }
}

signing {
    setRequired({
        gradle.taskGraph.hasTask("publish")
    })

    sign(publishing.publications["DigmaOtelAgentExtension"])
}

